dnl
dnl Copyright (c) 2006
dnl         The Xfce development team. All rights reserved.
dnl
dnl Originally written for Xfce by Benedikt Meurer <benny@xfce.org>
dnl Modified by Stephan Arts <stephan@xfce.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([squeeze_verinfo], [0:2:0])
m4_define([squeeze_version_branch], [])
m4_define([squeeze_version_major], [0])
m4_define([squeeze_version_minor], [1])
m4_define([squeeze_version_micro], [9])
m4_define([squeeze_version_nano], []) # Leave empty for no nano version
m4_define([squeeze_version_build], [r@REVISION@])
m4_define([squeeze_version_tag], [svn]) # Leave empty for releases
m4_define([squeeze_version], [ifelse(squeeze_version_branch(), [], [squeeze_version_major().squeeze_version_minor().squeeze_version_micro()ifelse(squeeze_version_nano(), [], [], [.squeeze_version_nano()])], [squeeze_version_branch()])ifelse(squeeze_version_tag(),[],[], [-ifelse(squeeze_version_tag(), [svn], [squeeze_version_tag()-squeeze_version_build()], [squeeze_version_tag()])])])

dnl *******************************************
dnl *** Debugging support for SVN snapshots ***
dnl *******************************************
m4_define([squeeze_debug_default], [ifelse(squeeze_version_tag(), [svn], [full], [no])])

AC_COPYRIGHT([Copyright (c) 2006
        The Xfce development team. All rights reserved.

Written for Xfce by Stephan Arts <stephan@xfce.org>.])

AC_INIT([Squeeze], [squeeze_version], [http://bugzilla.xfce.org/])
AC_PREREQ([2.50])

SQUEEZE_VERSION=squeeze_version
AM_INIT_AUTOMAKE([squeeze], [$SQUEEZE_VERSION])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

SQUEEZE_VERINFO=squeeze_verinfo
AC_SUBST(SQUEEZE_VERINFO)

dnl check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX
AM_CONDITIONAL([HAVE_CYGWIN], [test "`uname | grep \"CYGWIN\"`" != ""])

GTK_DOC_CHECK([1.0])


dnl check for standard header files
AC_PROG_CC
AC_PROG_LIBTOOL
AC_PROG_INTLTOOL([0.31], [no-xml])

AC_HEADER_STDC
AC_SYS_LARGEFILE
CPPFLAGS="$CPPFLAGS -D_FILE_OFFSET_BITS=64 -D_LARGE_FILES"

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
AC_ARG_ENABLE([debug],
AC_HELP_STRING([--enable-debug=@<:@no/minimum/yes/full@:>@], [Turn on debugging @<:@default=squeeze_debug_default@:>@]),
  [], [enable_debug=squeeze_debug_default])
AC_MSG_CHECKING([whether to enable debugging support])
if test x"$enable_debug" = x"full" -o x"$enable_debug" = x"yes"; then
  dnl Print the result
  AC_MSG_RESULT([$enable_debug])

  dnl Make sure we detect possible errors (if supported)
  save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS -Wall -Werror"
  AC_MSG_CHECKING([whether $CC accepts -Wall -Werror])
  AC_COMPILE_IFELSE(AC_LANG_SOURCE([int x;]), [
    AC_MSG_RESULT([yes])
    PLATFORM_CFLAGS="$PLATFORM_CFLAGS -Wall -Werror"
  ], [
    AC_MSG_RESULT([no])
  ])
  CFLAGS="$save_CFLAGS"

  dnl Paranoia for --enable-debug=full
  if test x"$enable_debug" = x"full"; then
    dnl Enable extensive debugging
    PLATFORM_CPPFLAGS="$PLATFORM_CPPFLAGS -DG_ENABLE_DEBUG"

    dnl Use -O0 -g3 if the compiler supports it
    save_CFLAGS="$CFLAGS"
    CFLAGS="$CFLAGS -O0 -g3"
    AC_MSG_CHECKING([whether $CC accepts -O0 -g3])
    AC_COMPILE_IFELSE(AC_LANG_SOURCE([int x;]), [
      AC_MSG_RESULT([yes])
      PLATFORM_CFLAGS="$PLATFORM_CFLAGS -O0 -g3"
    ], [
      AC_MSG_RESULT([no])
    ])
    CFLAGS="$save_CFLAGS"
  fi
else
  dnl Print the result
  AC_MSG_RESULT([$enable_debug])

  dnl Disable debugging (release build)
  PLATFORM_CPPFLAGS="$PLATFORM_CPPFLAGS -DNDEBUG"

  dnl Disable object cast checks
  PLATFORM_CPPFLAGS="$PLATFORM_CPPFLAGS -DG_DISABLE_CAST_CHECKS"

  dnl Disable all checks for --enable-debug=no
  if test x"$enable_debug" = x"no"; then
    PLATFORM_CPPFLAGS="$PLATFORM_CPPFLAGS -DG_DISABLE_ASSERT -DG_DISABLE_CHECKS"
  fi
fi

dnl Check for i18n support
XDT_I18N([@LINGUAS@])


XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.4.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.4.0])
XDT_CHECK_PACKAGE([GOBJECT], [gobject-2.0], [2.4.0])
XDT_CHECK_PACKAGE([THUNAR_VFS], [thunar-vfs-1], [0.4.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.3.99])

AC_DEFINE([HAVE_LIBXFCE4UTIL], [1], [Define if we have libxfce4util (will become optional eventually)])
AC_DEFINE([EXO_API_SUBJECT_TO_CHANGE], [1], [Define we understand libexo is subject to api churn])


dnl XDT_CHECK_OPTIONAL_PACKAGE([THUNAR_VFS], [thunar-vfs-1], [0.4.0], [thunar-vfs],
dnl    AC_HELP_STRING([--disable-thunar-vfs],
dnl                   [Disable the use of Thunar's VFS layer)]))

dnl XDT_CHECK_OPTIONAL_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.3.99], [libxfce4util],
dnl    AC_HELP_STRING([--disable-libxfce4util],
dnl                   [Disable the use of libxfce4util)]))

AC_ARG_ENABLE([pathbar],
   [AC_HELP_STRING([--disable-pathbar], 
        [Do not compile support for a PathBar (default=enabled)])],
        [sq_pathbar=$enableval], 
        [sq_pathbar=yes])
if test "$sq_pathbar" = "yes"; then
	AC_DEFINE([ENABLE_PATHBAR], [1], [Define if we should include pathbar support])
fi
AM_CONDITIONAL([ENABLE_PATHBAR], [test "$sq_pathbar" = "yes"])

AC_ARG_ENABLE([toolbar],
   [AC_HELP_STRING([--disable-toolbar], 
        [Do not compile support for a ToolBar (default=enabled)])],
        [sq_toolbar=$enableval], 
        [sq_toolbar=yes])
if test "$sq_toolbar" = "yes"; then
	AC_DEFINE([ENABLE_TOOLBAR], [1], [Define if we should include toolbar support])
fi
AM_CONDITIONAL([ENABLE_TOOLBAR], [test "$sq_toolbar" = "yes"])

dnl check for debugging support
XDT_FEATURE_DEBUG

AC_OUTPUT([
Makefile
po/Makefile.in
src/Makefile
libsqueeze/Makefile
icons/Makefile
icons/16x16/Makefile
icons/48x48/Makefile
icons/scalable/Makefile
pixmaps/Makefile
docs/Makefile
docs/reference/Makefile
docs/reference/libsqueeze/Makefile
docs/reference/libsqueeze/version.xml
tests/Makefile
tests/zip/Makefile
])

echo "----------------------------------------"
echo "$PACKAGE $VERSION"
echo ""
echo "The binary will be installed in $prefix/bin"
echo ""
echo "Configure finished, type 'make' to build."
